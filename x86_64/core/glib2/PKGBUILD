pkgname=glib2
pkgver=2.72.2
pkgrel=1
arch=('x86_64')
depends=('libffi' 'pcre' 'util-linux' 'zlib' 'elfutils' 'libxslt' 'libxml2')
makedepends=('gtk-doc' 'python3-pip' 'dbus' 'gettext' 'git' 'meson' 'docbook-xml' 'docbook-xsl')
source=(git+https://gitlab.gnome.org/GNOME/glib.git#tag=$pkgver
	noisy-glib-compile-schemas.diff
	glib-compile-schemas.hook
	gio-querymodules.{hook,script})

prepare() {
	cd glib
	git apply -3 ../noisy-glib-compile-schemas.diff

	pip3 install pygments
}

build() {
	CFLAGS+=" -DG_DISABLE_CAST_CHECKS"

	meson glib build		\
	-D glib_debug=disabled		\
	-D selinux=disabled		\
	-D man=false			\
	-D gtk_doc=true			\
	--prefix=/usr			\
	--libdir=/usr/lib

	meson compile -C build
}

package() {
	meson install -C build --destdir $pkgdir

	python3 -m compileall -d /usr/share/glib-2.0/codegen \
	$pkgdir/usr/share/glib-2.0/codegen

	python3 -O -m compileall -d /usr/share/glib-2.0/codegen \
        $pkgdir/usr/share/glib-2.0/codegen

	mkdir -p $pkgdir/usr/lib/pkgconfig
	cp -f build/meson-private/*.pc $pkgdir/usr/lib/pkgconfig

	install -Dt $pkgdir/usr/share/libalpm/hooks/ -m644 *.hook
	install -D gio-querymodules.script $pkgdir/usr/share/libalpm/scripts/gio-querymodules
}
